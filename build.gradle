import org.jetbrains.dokka.gradle.DokkaTask

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id 'org.jetbrains.dokka' version '1.7.10'
    id 'maven-publish'
    id 'java-library'
    id 'signing'
    id 'jacoco'
}

group = 'dev.roava'

ext {
    slf4jVersion = '2.0.16'
    junitVersion = '5.11.4'
    jacksonVersion = '2.13.4.2'
    retrofitVersion = '2.11.0'

    isReleaseVersion = !project.version.toString().endsWith("SNAPSHOT")
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    implementation "org.slf4j:slf4j-log4j12:$slf4jVersion"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-jackson:$retrofitVersion"

    compileOnly 'org.jetbrains.dokka:dokka-base:1.7.10'
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from(components["java"])

            pom {
                name = 'roava'
                description = 'A unique Roblox API wrapper written in Java'
                url = 'https://github.com/RoavaDev/Roava'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://mit-license.org/'
                        distribution = 'repo'
                    }
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/StayBlueee/Roava/issues'
                }

                scm {
                    url = 'https://github.com/RoavaDev/Roava'
                    connection = 'scm:git:git://github.com/RoavaDev/Roava.git'
                    developerConnection = 'scm:git:git://github.com:Roava/Roava.git'
                }

                developers {
                    developer {
                        name = 'Roava Development Team'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def mavenUsername = findProperty('mavenUsername')
            def mavenPassword = findProperty('mavenPassword')

            if(isReleaseVersion) {
                url = uri('https://m.cpy.wtf/releases')
            } else {
                url = uri('https://m.cpy.wtf/snapshots')
            }

            if (mavenUsername != null && mavenPassword != null) {
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            }
        }
    }


}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

tasks.named("dokkaHtml", DokkaTask) {
    suppressInheritedMembers.set(true)
}

java {
    withJavadocJar()
    withSourcesJar()
}

jacocoTestReport() {
    reports {
        xml.required = true
        html.required = true
    }
}

test.finalizedBy(jacocoTestReport)